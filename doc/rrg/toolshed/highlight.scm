(define-record-type achar
  (adorn-char char kind mate mesg)
  adorned-char?
  (char get-char set-char!)
  (kind get-kind set-kind!)
  (mate get-mate set-mate!)
  (mesg get-mesg set-mesg!))

(define (char-in char list-of-chars)
  (member char list-of-chars char=?))

(define (fresh-kind c)
  (cond ((char=? c #\.)                            'dot)
        ((char=? c #\|)                            'identifier)
        ((char=? c #\\)                            'infix)
        ((char=? c #\;)                            'line-comment)
        ((char=? c #\()                            'list)
        ((char=? c #\")                            'string)
        ((char-in c '( #\' #\` #\, ))              'abbrev)
        ((char-in c '( #\{ #\} ))                  'brace)
        ((char-in c '( #\[ #\] ))                  'bracket)
        ((char-in c '( #\newline #\space #\tab ))  'whitespace)
        (else                                      'tbd)))

(define (fresh-message c)
  (cond ((char=? c #\|)  'identifier)
        ((char=? c #\,)  'abbrev-if-@)
        ((char=? c #\#)  'octothorpe-begin)
        ((char=? c #\")  'string)
        ((char=? c #\;)  'line-comment)
        (else            #f)))

(define (refresh! ac)
  (let ((c (get-char ac)))
    (set-kind! ac (fresh-kind c))
    (set-mesg! ac (fresh-message c))))

(define (invalidate! ac)
  (set-kind! ac 'invalid)
  (set-mesg! ac 'invalid))
;-----------------------------------------------------------------------------;
(define (digit2?  c) (char-in c '( #\0 #\1 )))
(define (digit8?  c) (char-in c '( #\0 #\1 #\2 #\3 #\4 #\5 #\6 #\7 )))
(define (digit10? c) (char-in c '( #\0 #\1 #\2 #\3 #\4 #\5 #\6 #\7 #\8 #\9 )))
(define (digit16? c) (char-in c '( #\0 #\1 #\2 #\3 #\4 #\5 #\6 #\7 #\8 #\9
                                   #\a #\b #\c #\d #\e #\f
                                   #\A #\B #\C #\D #\E #\F )))
;-----------------------------------------------------------------------------;
(define (delimiter? c)
  (char-in c '( #\| #\( #\) #\" #\; #\newline #\space #\tab )))

(define (exactness-char? c)
  (char-in c '( #\e #\i )))

(define (exponent-or-precision-char? c)
  (char-in c '( #\e #\s #\f #\d #\l #\E #\S #\F #\D #\L )))

(define (radix-char? c)
  (char-in c '( #\x #\d #\o #\b )))
;-----------------------------------------------------------------------------;
(define (preceding-tbd ac-list)
  (list->string (let loop ((new '()) (old ac-list))
                  (if (null? old)
                      new
                      (let ((first (car old)))
                        (if (eq? (get-kind first) 'tbd)
                            (loop (cons (get-char first) new)
                                  (cdr old))
                            new))))))

(define (tbd-in token ac-list)
  (member
   (preceding-tbd ac-list)
   (cadr (assq token
               '((char ("#\\null" "#\\alarm" "#\\backspace" "#\\tab" "#\\space"
                        "#\\newline" "#\\return" "#\\delete" "#\\escape"
                        "#\\page" "#\\vtab" "#\\esc" "#\\linefeed" "#\\nul"))
                 (directive ("#!fold-case" "#!no-fold-case"))
                 (false ("#f" "#false"))
                 (fvector ("#f32" "#f64"))
                 (runtime-syntax ("and" "begin" "c-declare" "c-define"
                                  "c-define-type" "c-initialize" "c-lambda"
                                  "case" "case-lambda" "cond" "cond-expand"
                                  "declare" "define" "define-library"
                                  "define-macro" "define-record-type"
                                  "define-structure" "define-syntax"
                                  "define-type" "define-type-of-thread"
                                  "define-values" "delay" "delay-force" "do"
                                  "future" "guard" "if" "import" "include"
                                  "include-ci" "lambda" "let" "let*"
                                  "let*-values" "let-values" "letrec" "letrec*"
                                  "letrec*-values" "letrec-values" "namespace"
                                  "or" "parameterize" "quasiquote" "quote"
                                  "r7rs-guard" "receive" "set!" "syntax-error"
                                  "syntax-rules" "this-source-file" "unless"
                                  "when"))
                 (sharp ("#!eof" "#!key" "#!optional" "#!rest" "#!void"))
                 (svector ("#s8" "#s16" "#s32" "#s64"))
                 (true ("#t" "#true"))
                 (uvector ("#u8" "#u16" "#u32" "#u64")))))
   string=?))

(define (vector-string->symbol s)
  (string->symbol (string-append (string-copy s 1) "vector")))

(define (mate nac mate-char ac-list)
  (let ((empty? #t))
    (let ((find-mate (lambda ()
                       (let loop ((rest ac-list))
                         (if (null? rest)
                             (values 'invalid #f)
                             (let ((ac (car rest)))
                               (let ((ak (get-kind ac))
                                     (amesg (get-mesg ac)))
                                 (if (and (number? amesg)
                                          (not (get-mate ac))
                                          (char=? (get-char ac) mate-char))
                                     (begin (when empty?
                                              (set-kind! ac
                                                         (string->symbol
                                                          (string-append
                                                           (symbol->string ak)
                                                           "-empty"))))
                                            (set-mate! ac (get-mesg nac))
                                            (values (get-kind ac) amesg))
                                     (begin (unless (eq? ak 'whitespace)
                                              (set! empty? #f))
                                            (loop (cdr rest))))))))))
          (record-mate (lambda (kind message)
                         (set-kind! nac kind)
                         (set-mate! nac message))))
      (call-with-values find-mate record-mate))))

(define (revise-kinds! ac-list new-kind . optional-old-kind)
  (call-with-current-continuation
   (lambda (done)
     (let ((old-kind (if (null? optional-old-kind)
                         'tbd
                         (car optional-old-kind))))
       (let loop ((rest ac-list))
         (if (null? rest)
             #t
             (let ((ac (car rest)))
               (if (eq? (get-kind ac) old-kind)
                   (begin (set-kind! ac new-kind)
                          (loop (cdr rest)))
                   (done new-kind)))))))))

(define (adorn-list char-list)
  (let loop ((adorned '()) (unadorned char-list) (adorned-length 0))
    (if (null? unadorned)
        (reverse adorned)
        (loop (cons (adorn! (car unadorned) adorned adorned-length) adorned)
              (cdr unadorned)
              (+ adorned-length 1)))))

(define (adorn! next-unadorned-char rest adorned-length)
  (let ((nac (adorn-char next-unadorned-char 'tbd #f #f)))
    (if (null? rest)
        (begin (refresh! nac)
               (when (char=? (get-char nac) #\()
                 (set-mesg! nac adorned-length)))
        (let ((nc (get-char nac))
              (pac (car rest)))
          (let ((pc (get-char pac))
                (pk (get-kind pac))
                (pm (get-mesg pac)))
            (cond ((not (symbol? pm))
                   (refresh! nac)
                   (when (delimiter? nc)
                     (cond ((char=? #\( nc)
                            (set-mesg! nac adorned-length))
                           ((char=? #\) nc)
                            (set-mesg! nac adorned-length)
                            (mate nac #\( rest))
                           ((char=? #\: nc)
                            (if (not (delimiter? pc))
                                (revise-kinds! rest 'keyword)
                                (revise-kinds! rest 'default)))
                           ((tbd-in 'runtime-syntax rest)
                            (revise-kinds! rest 'runtime-syntax))
                           (else (revise-kinds! rest 'default)))))
                  ((eq? pm 'invalid)
                   (if (delimiter? nc)
                       (refresh! nac)
                       (invalidate! nac)))
                  ((eq? pm 'identifier)
                   (when (char=? pc #\|)
                     (let ((ddr (cdr rest)))
                       (when (or (null? ddr)
                                 (not (eq? (get-kind (car ddr)) 'identifier)))
                         (set-mesg! pac (- adorned-length 1)))))
                   (set-kind! nac 'identifier)
                   (if (and (char=? nc #\|) (not (char=? pc #\\)))
                       (begin (set-mesg! nac adorned-length)
                              (mate nac #\| rest))
                       (set-mesg! nac 'identifier)))
                  ((eq? pm 'line-comment)
                   (if (char=? nc #\newline)
                       (refresh! nac)
                       (begin (set-kind! nac 'line-comment)
                              (set-mesg! nac 'line-comment))))
                  ((eq? pm 'string)                   
                   (when (char=? pc #\")
                     (let ((ddr (cdr rest)))
                       (when (or (null? ddr)
                                 (not (eq? (get-kind (car ddr)) 'string)))
                         (set-mesg! pac (- adorned-length 1)))))
                   (set-kind! nac 'string)
                   (if (and (char=? nc #\") (not (char=? pc #\\)))
                       (begin (set-mesg! nac adorned-length)
                              (mate nac #\" rest))
                       (set-mesg! nac 'string)))
                  ((eq? pm 'char-begin)
                   (set-mesg! nac 'char-first))
                  ((eq? pm 'char-first)
                   (if (delimiter? nc)
                       (begin (revise-kinds! rest 'char)
                              (refresh! nac))
                       (set-mesg! nac 'named-char)))
                  ((eq? pm 'named-char)
                   (if (delimiter? nc)
                       (begin (revise-kinds! rest (if (tbd-in 'char rest)
                                                      'char
                                                      'invalid))
                              (refresh! nac))
                       (set-mesg! nac 'named-char)))
                  ((eq? pm 'directive-or-sharp)
                   (if (delimiter? nc)
                       (begin (refresh! nac)
                              (if (tbd-in 'directive rest)
                                  (revise-kinds! rest 'directive)
                                  (if (tbd-in 'sharp rest)
                                      (revise-kinds! rest 'sharp)
                                      (revise-kinds! rest 'invalid))))
                       (set-mesg! nac 'directive-or-sharp)))
                  ((memq pm '(svector uvector))
                   (if (delimiter? nc)
                       (if (char=? nc #\()
                           (let ((tbd (tbd-in pm rest)))
                             (if tbd
                                 (let ((nk (vector-string->symbol (car tbd))))
                                   (begin (set-kind! nac nk)
                                          (set-mesg! nac adorned-length)
                                          (revise-kinds! rest nk)))
                                 (begin (refresh! nac)
                                        (revise-kinds! rest 'invalid))))
                           (begin (refresh! nac)
                                  (revise-kinds! rest 'invalid)))
                       (set-mesg! nac pm)))
                  ((eq? pm 'false-or-fvector)
                   (if (delimiter? nc)
                       (if (char=? nc #\()
                           (let ((tbd (tbd-in 'fvector rest)))
                             (if tbd
                                 (let ((nk (vector-string->symbol (car tbd))))
                                   (begin (set-kind! nac nk)
                                          (set-mesg! nac adorned-length)
                                          (revise-kinds! rest nk)))
                                 (begin (refresh! nac)
                                        (revise-kinds! rest 'invalid))))
                           (begin (refresh! nac)
                                  (revise-kinds! rest (if (tbd-in 'false rest)
                                                          'boolean
                                                          'invalid))))
                       (set-mesg! nac 'false-or-fvector)))
                  ((eq? pm 'true)
                   (if (delimiter? nc)
                       (begin (revise-kinds! rest (if (tbd-in 'true rest)
                                                      'boolean
                                                      'invalid))
                              (refresh! nac))
                       (set-mesg! nac 'true)))
                  ((eq? pm 'octothorpe-begin)
                   (cond ((char=? nc #\&)
                          (set-kind! nac (revise-kinds! rest 'box)))
                         ((char=? nc #\\)
                          (set-mesg! nac 'char-begin))
                         ((char=? nc #\|)
                          (set-kind! nac 'nested-comment)
                          (set-mesg! nac 'nested-comment))
                         ((char=? nc #\f)
                          (set-mesg! nac 'false-or-fvector))
                         ((char=? nc #\s)
                          (set-mesg! nac 'svector))
                         ((char=? nc #\t)
                          (set-mesg! nac 'true))
                         ((char=? nc #\u)
                          (set-mesg! nac 'uvector))
                         ((digit10? nc)
                          (set-mesg! nac 'label-or-reference-or-serial))
                         ((char=? nc #\!)
                          (set-mesg! nac 'directive-or-sharp))
                         ((exactness-char? nc)
                          (set-mesg! nac 'radix-or-digit10))
                         ((char=? nc #\()
                          (set-kind! nac 'vector)
                          (set-mesg! nac adorned-length)
                          (revise-kinds! rest 'vector))
                         ((radix-char? nc)
                          (set-mesg! nac ((cond ((char=? nc #\x)
                                                 'exactness-or-digit16)
                                                ((char=? nc #\d)
                                                 'exactness-or-digit10)
                                                ((char=? nc #\o)
                                                 'exactness-or-digit8)
                                                ((char=? nc #\b)
                                                 'exactness-or-digit2)
                                                (else 'invalid)))))
                         ((char=? nc #\;)
                          (set-kind! nac 'datum-comment)
                          (set-mesg! 'datum-comment))
                         (else
                          (invalidate! nac))))
                  (else (refresh! nac))))))
    nac))
